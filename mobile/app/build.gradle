plugins {
    id 'com.android.application'
}

android {
    ndkVersion "28.2.13676358"
    compileSdk 35

    def appVersionCode = System.getenv("APPLICATION_VERSION_CODE") ?: "1"
    def appVersionName = System.getenv("APPLICATION_VERSION_NAME") ?: "1.0.0"
    defaultConfig {
        applicationId "pe.nikescar.uad_shizuku"
        minSdk 24
        targetSdk 35
        versionCode appVersionCode.toInteger()
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Load keystore properties
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        // debug {
        //     storeFile file("debug.keystore")
        //     storePassword "android"
        //     keyAlias "androiddebugkey"
        //     keyPassword "android"
        // }
        
        release {
            if (keystorePropertiesFile.exists()) {
                // Use keystore.properties file
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                // Use environment variables as fallback
                def envStorePassword = System.getenv("KEYSTORE_PASSWORD")
                def envKeyPassword = System.getenv("KEY_PASSWORD")
                
                if (envStorePassword && envKeyPassword) {
                    storeFile file("release.keystore")
                    storePassword envStorePassword
                    keyAlias "release"
                    keyPassword envKeyPassword
                }
                // If neither keystore.properties nor env vars exist, release will be unsigned
            }
        }
    }

    buildTypes {
        // debug {
        //     minifyEnabled false
        //     signingConfig signingConfigs.debug
        //     //packagingOptions {
        //     //    doNotStrip '**/*.so'
        //     //}
        //     //debuggable true
        // }
        
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists() || (System.getenv("KEYSTORE_PASSWORD") && System.getenv("KEY_PASSWORD"))) {
                signingConfig signingConfigs.release
            }
            // If no signing config available, release will be unsigned
        }
    }


    if (System.getenv("ANDROID_SPLIT_BUILD") == "1") {
        splits {
            abi {
                enable true
                reset()
                include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64' // Specify desired ABIs
            universalApk false // Set to true to also build a universal APK
            }
        }
    }
    
    buildFeatures {
        aidl true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    
    namespace 'pe.nikescar.uad_shizuku'
}

dependencies {
    implementation 'dev.rikka.shizuku:api:13.1.5'
    implementation 'dev.rikka.shizuku:provider:13.1.5'
}

// Disable all lint tasks to work around current lint crashes in build environment.
tasks.matching { it.name.toLowerCase().contains("lint") }.configureEach {
    enabled = false
}

